 R←Test_Misc_006(stopFlag batchFlag);⎕TRAP;htmlFilename;TEMP;C;html
⍝ Checks what happens if a single line got executed
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')

 R←T._Failed

 Data.Prepare
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'r←Hello' 'r←''Hello world''' '→0' 'a←1'

 C←⎕NEW ##.CodeCoverage(,⊂'#.TEMP')
 C.Start ⍬
 {}#.TEMP.Hello
 C.Stop ⍬
 {}C.Finalise ⍬
 →T.GoToTidyUp 1≠≢C.data
 {}1 ##.CodeCoverage.ProcessDataAndCreateReport C.filename
 html←⊃⎕NGET C.filename,'.html'
 →T.GoToTidyUp 0=+/'>→[3] '⍷html
 →T.GoToTidyUp 1≠≢'>→\[[0-9]+] '⎕S 0⊣html

 R←T._OK

∆TidyUp:
1 T.codeCoverage.DeleteFiles Data.T.codeCoverage.filename
 Data.⎕EX'T'
⍝Done
