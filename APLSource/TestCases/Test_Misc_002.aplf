 R←Test_Misc_002(stopFlag batchFlag);⎕TRAP;htmlFilename;tno;result;toc;b;row;name;could;did
⍝ Just runs stuff in a script and then checks the coverage results
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 Data.Prepare
 Data.T.codeCoverage.filename←(739⌶0),'/CodeCoverage/',(⊃⎕SI)
 3 ⎕MKDIR⊃⎕NPARTS Data.T.codeCoverage.filename
 {}Data.T.RunThese'Test_002'
 {}Data.T.RunThese'Test_002'
 htmlFilename←##.CodeCoverage.ProcessDataAndCreateReport Data.T.codeCoverage.filename
 tno←(Data.T.codeCoverage.filename,'.profile')⎕FTIE 0
 →T.GoToTidyUp 12≠2⊃⎕FSIZE tno                ⍝ First 9 are reserved, 10+11 hold data (two times!)

 result←⎕FREAD tno,10
 toc←↑result[;1]~¨' '
 b←∨/'∆Script1'⍷toc
 (toc result)←b∘⌿¨toc result
 :For row :In ⍳≢toc
     name←' '~⍨toc[row;]
     :If '#.CodeCoverage.TestCases.Data.∆Script1.Hello2'≡name
         ⍝TODO⍝ To be ignored for the time being because ⎕PROFILE cannot deal with names local dfns inside tfns in any meaningful way
     :Else
         (could did)←∆GetLinesFromScrips name
         →T.GoToTidyUp did could≢result[row;2 4]
     :EndIf
 :EndFor

 R←T._OK

∆TidyUp:
 ⎕FUNTIE tno
 1 Data.T.codeCoverage.DeleteFiles Data.T.codeCoverage.filename
 {}FilesAndDirs.RmDirByForce⊃⎕NPARTS Data.T.codeCoverage.filename
 Data.⎕EX'T'
⍝Done
